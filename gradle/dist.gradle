javadoc {
    classpath = configurations.compileClasspath
    title = "${project.name} API Documentation $version"
    failOnError = true
    source = sourceSets.main.allJava
    options.optionFiles = [file("gradle/javadoc.options")]
}
task copyJavadocToDist(type: Copy) {
    includeEmptyDirs = false
    from javadoc
    into "${buildDir}/${project.name}/doc"
    dependsOn javadoc
}
task copyDocAndConfigFilesToDist(type: Copy) {
    includeEmptyDirs = false
    from('src/main/resources') { include '*-template.yaml' }
    from('.') { include 'README.md' }
    into "${buildDir}/${project.name}"
}
task copySbomToDist(type: Copy) {
    from('build/reports') { include 'bom.*'}
    into "${buildDir}/${project.name}/sbom"
    dependsOn cyclonedxBom
}
task copyLibsToDist(type: Copy) {
    includeEmptyDirs = false
    from jar
    include '**/*.jar'
    exclude "${project.name}*.jar"
    from sourceSets.main.runtimeClasspath
    into "${buildDir}/${project.name}/lib"
    dependsOn jar, copyDocAndConfigFilesToDist, copyJavadocToDist, copySbomToDist
}
task copyMainLibToDist(type: Copy) {
    includeEmptyDirs = false
    from jar
    include "${project.name}*.jar"
    from sourceSets.main.runtimeClasspath
    into "${buildDir}/${project.name}"
    dependsOn jar, copyDocAndConfigFilesToDist, copyJavadocToDist, copySbomToDist
}
task dist(type: Zip) {
    group = 'build'
    description("Creates the ${project.name}.zip with all jars")
    from("${buildDir}") {
        include "${project.name}/**"
    }
    archiveFileName = "${buildDir}/${project.name}.zip"
    dependsOn copyLibsToDist, copyMainLibToDist, copyDocAndConfigFilesToDist, copyJavadocToDist, copySbomToDist
}

